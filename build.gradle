// Top-level build file where you can add configuration options common to all sub-projects/modules.
ext {
	// Author
	developerId = "davideas"
	developerName = "Davide Steduto"
	developerEmail = "dave.dna@gmail.com"

	// Support and Build tools version
	minSdk = 19
	targetSdk = 25
	appCode = 1
	appVersion = "1.0"
	appDate = " built on " + getDate()
	buildCode = 1
	buildTools = "25.0.2"
	supportLib = "25.3.1"

	// Support Libraries dependencies
	supportDependencies = [
			design         : "com.android.support:design:${supportLib}",
			recyclerview   : "com.android.support:recyclerview-v7:${supportLib}",
			cardview       : "com.android.support:cardview-v7:${supportLib}",
			appcompat      : "com.android.support:appcompat-v7:${supportLib}",
			customtabs     : "com.android.support:customtabs:${supportLib}",
			support_v13    : "com.android.support:support-v13:${supportLib}",
			annotations    : "com.android.support:support-annotations:${supportLib}",
			vector_drawable: "com.android.support:support-vector-drawable:${supportLib}",
			preference_v7  : "com.android.support:preference-v7:${supportLib}",
			preference_v14 : "com.android.support:preference-v14:${supportLib}"
	]
}

buildscript {
	repositories {
		jcenter()
	}
	dependencies {
		classpath 'com.android.tools.build:gradle:2.3.2'

		// NOTE: Do not place your application dependencies here; they belong
		// in the individual module build.gradle files
	}
}

static def getDate() {
	def date = new Date()
	return date.format('yyyy.MM.dd')
}

allprojects {
	repositories {
		jcenter()
		maven {url 'https://oss.sonatype.org/content/repositories/snapshots/' }
		maven {url 'https://maven.google.com'}
	}
}

task clean(type: Delete) {
	delete rootProject.buildDir
}
