apply plugin: 'com.android.application'

android {
	compileSdkVersion targetSdk
	buildToolsVersion buildTools
	defaultConfig {
		applicationId 'eu.davidea.starterapp'
		minSdkVersion minSdk
		targetSdkVersion targetSdk
		versionCode appCode
		versionName appVersion + appDate

		// https://developer.android.com/studio/write/vector-asset-studio.html
		vectorDrawables.useSupportLibrary = true

		testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'

		applicationVariants.all { variant ->
			variant.outputs.each { output ->
				def file = output.outputFile
				def fileName = 'starter-app-v' + appVersion + '-c' + buildCode + '.apk'
				output.outputFile = new File(file.parentFile, fileName)
			}
		}
	}
	buildTypes {
		debug {
			versionNameSuffix ' build'
			minifyEnabled false
		}
		release {
			zipAlignEnabled true
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}
	}
	testOptions {
		unitTests.returnDefaultValues = true
	}

	dataBinding {
		enabled = true
	}
}

dependencies {
	compile fileTree(dir: 'libs', include: ['*.jar'])

	// Test
	androidTestCompile supportDependencies.annotations
	androidTestCompile 'com.android.support.test:runner:0.5'
	androidTestCompile 'com.android.support.test:rules:0.5'
	androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
		exclude group: 'com.android.support', module: 'support-annotations'
	})
	testCompile 'org.robolectric:robolectric:3.1.2'
	testCompile 'junit:junit:4.12'
	testCompile 'org.mockito:mockito-core:2.7.22'

	// Support Libraries
	compile 'com.android.support.constraint:constraint-layout:1.1.0-beta1'
	compile supportDependencies.appcompat
	compile supportDependencies.cardview
	compile supportDependencies.support_v13
	compile supportDependencies.preference_v7
	compile supportDependencies.preference_v14
	compile supportDependencies.vector_drawable
	compile (supportDependencies.design) {
		exclude module: 'support-v4'
		exclude module: 'support-annotations'
	}
	compile (supportDependencies.recyclerview) {
		exclude module: 'support-v4';
		exclude module: 'support-annotations'
	}

	// Flexible Adapter
	compile 'eu.davidea:flexible-adapter:5.0.0-SNAPSHOT'

	// Google Maps
	compile 'com.google.android.gms:play-services-maps:10.2.1'

	// Glide (Image Loader & Caching)
	compile 'com.github.bumptech.glide:glide:4.0.0-RC0'
	annotationProcessor 'com.github.bumptech.glide:compiler:4.0.0-RC0'

	// Butter Knife (View Injector)
	compile 'com.jakewharton:butterknife:8.5.1'
	annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'

	// UI Widgets
	compile 'eu.davidea:flipview:1.1.3'

}